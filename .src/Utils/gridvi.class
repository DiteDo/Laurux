' Gambas class file

Private $grdv As GridView
Private $lab As Label
Private $sdec As String
Private $scode As String
Private $stype As String

Property code As String
Property dec As String

Public Sub _new(TabStrip1 As TabStrip, type As String)        'dessin du gridview et des colonnes
  
  Dim res As Result
  Dim sdte As String[]
  Dim i, j, k As Integer
  Dim pan As Panel
  'Dim but As Button
  
  $stype = Type
  
  TabStrip1.Count += 1
  TabStrip1.Current.Text = "Statistiques"
  
  pan = New Panel(TabStrip1)
  pan.Height = 24
  pan.Width = TabStrip1.Width
  pan.Arrangement = Arrange.Horizontal
  pan.x = 0
  pan.Y = 0
  
  $lab = New Label(pan)
  $lab.Height = 24
  $lab.text = "TABLEAU COMPARATIF ANNUEL"
  $lab.Alignment = Align.Center
  $lab.AutoResize = True
  $lab.Expand = True
  $lab.Background = &HDFBFFF
  
  'but = New Button(pan) As "bouton"
  'but.Height = 24
  'but.Text = "Calcul"
  'but.AutoResize = True
  'but.Expand = True
  'but.Picture = Picture["Icones/find.png"]
  
  $grdv = New GridView(TabStrip1)
  $grdv.Height = TabStrip1.Height - 30 - 24
  $grdv.Width = TabStrip1.Width - 10
  $grdv.x = 0
  $grdv.y = 25
  
  $grdv.Header = GridView.Both
  $grdv.ScrollBar = Scroll.Both
  
  res = utils.db.Exec("SELECT * FROM Fiches_Parametres")
  If Not res.Available Then Return
  
  sdte = Split(res!dteclec, "/")
  $grdv.Columns.Count = 12
  $grdv.Rows.Count = 13
  
  k = 0
  For i = 0 To 3
    For j = 1 To 3
      $grdv.Columns[k].Alignment = Align.Center
      Select j
        Case 1 
          $grdv.Columns[k].Width = 110
          If sdte[0] <> "12" Then
            $grdv.Columns[k].Title = Str(Val(sdte[2]) - i) & "/" & Str(Val(sdte[2]) - (i + 1))
            $grdv.Columns[k].text &= " C.A"
          Else
            $grdv.Columns[k].text = Str(Val(sdte[2]) - i) 
            $grdv.Columns[k].text &= " C.A"
          Endif
        Case 2
          $grdv.Columns[k].text = "Quantit√©"
          $grdv.Columns[k].Width = 90
        Case 3
          $grdv.Columns[k].text = "Marge"
          $grdv.Columns[k].Width = 80
      End Select
      
      k += 1
    Next
  Next
  
  j = sdte[0]
  For i = 0 To 11
    j += 1
    If j = 13 Then j = 1
    $grdv.Rows[i].Text = utils.mois(j)
    $grdv.rows[i].Height = 18
    
  Next
  $grdv.Rows[12].Text = "TOTAL"
  $grdv.Rows[12].Height = 22
  TabStrip1.Index = 0
  
End

Public Sub bouton_Click()         'lit et affiche les stat
  
  Dim respar, resbl, resstat As Result
  Dim i, j As Integer
  Dim smoiscour, smoisprec As String
  Dim dte As String[]
  Dim fsomme As Float
  Dim smois, sanne, scle As String
  Dim dateinf, datesup As String
  Dim tab, tab1, req, cond As String
  
  $grdv.Parent.Mouse = mouse.Wait
  $lab.text = "CALCUL EN COURS"
  $lab.Background = &HDFBFFF
  '                                                             lit les parametres
  respar = utils.db.Exec("SELECT * FROM Fiches_Parametres")
  If Not respar.Available Then Return
  
  smoiscour = Left(respar!dtepec, 2) & Right(respar!dtepec, 4)
  smoisprec = Left(respar!dtepp, 2) & Right(respar!dtepp, 4)
  dte = Split(respar!dteclec, "/")
  '                                                             lit le fichier facture mois par mois
  smois = dte[0]
  If smois = "12" Then
    smois = "01"
    sanne = dte[2]
  Else
    smois = Format(Str(Val(smois) + 1), "00")
    sanne = dte[2] - 1
  Endif
  
  i = 0
  Repeat
    j = 0
    Repeat
      
      scle = smois & sanne
      dateinf = sanne & "-" & smois & "-00"
      datesup = sanne & "-" & smois & "-" & Str(utils.lastday(Val(dateinf)))
      tab = "Fiches_HistoLigfac"
      tab1 = "Fiches_HistoFac"
      Select $stype
        Case "F"
          req = "mrgart_ligfac AS marge,mrgmo_ligfac AS margem,netht_ligfac AS ht,qte_ligfac AS qte,typel_ligfac AS type"
          cond = "fam_ligfac=&1 AND numfac=num_ligfac AND datefac BETWEEN &2 AND &3"
        Case "A"
          req = "mrgart_ligfac AS marge,netht_ligfac AS ht,qte_ligfac AS qte,typel_ligfac AS type"
          cond = "code_ligfac=&1 AND numfac=num_ligfac AND datefac BETWEEN &2 AND &3"
        Case "M"
          req = "mrgmo_ligfac AS margem,netht_ligfac AS ht,qte_ligfac AS qte,typel_ligfac AS type"
          cond = "code_ligfac=&1 and numfac=num_ligfac AND datefac BETWEEN &2 AND &3"
      End Select
      
      $grdv[j, i].Text = "0.00"
      $grdv[j, i + 1].Text = $sdec
      $grdv[j, i + 2].Text = "0.00"
      requete(tab, tab1, req, cond, dateinf, datesup, i, j)
      
      '                                                   lit et additionne les mois en cours dans le fichier bl
      If scle = smoiscour Or scle = smoisprec Then
        tab = "Fiches_Ligbl"
        tab1 = "Fiches_Bl"
        
        Select $stype
          Case "F"
            req = "mrgart_ligbl AS marge,mrgmo_ligbl AS margem,netht_ligbl AS ht,qte_ligbl AS qte,typel_ligbl AS type"
            cond = "fam_ligbl=&1 and numbl=num_ligbl AND datebl BETWEEN &2 AND &3"
          Case "A"
            req = "mrgart_ligbl AS marge,netht_ligbl AS ht,qte_ligbl AS qte"
            cond = "code_ligbl=&1 and numbl=num_ligbl AND datebl BETWEEN &2 AND &3"
          Case "M"
            req = "mrgmo_ligbl AS marge,netht_ligbl AS ht,qte_ligbl AS qte"
            cond = "code_ligbl=&1 and numbl=num_ligbl AND datebl BETWEEN &2 AND &3"
        End Select
        requete(tab, tab1, req, cond, dateinf, datesup, i, j)
        
      Endif
      
      smois = Format(Str(Val(smois) + 1), "00")
      If smois = "13" Then
        smois = "01"
        sanne += 1
      Endif
      
      j += 1
    Until j = 12
    
    sanne -= 2
    i += 3
  Until i = 12
  '                                       affiche les totaux de colonne
  For i = 0 To 11
    fsomme = 0
    For j = 0 To 11
      Try fsomme += CFloat(Replace($grdv[j, i].Text, ",", "."))
    Next
    
    Select i
      Case 1, 4, 7, 10
        $grdv[12, i].Text = Format(fsomme, "######" & $sdec)
      Case Else
        $grdv[12, i].Text = Format(fsomme, "#######0.00")
    End Select
    
  Next
  
  $grdv.Parent.Mouse = Mouse.Default
  $lab.text = "TABLEAU COMPARATIF ANNUEL"
  
End

Private Sub requete(tab As String, tab1 As String, req As String, cond As String, dateinf As String, datesup As String, i As Integer, j As Integer)
  
  Dim resbl As Result
  Dim k As Integer
  Dim fsomme As Float
  
  resbl = utils.db.Exec("SELECT " & req & " FROM " & tab & "," & tab1 & " WHERE " & cond, $scode, dateinf, datesup)
  If Not resbl.Available Then Return
  
  resbl.MoveFirst
  For k = 0 To resbl.Max
    fsomme = 0
    Try fsomme = CFloat(Replace($grdv[j, i].Text, ",", ".")) + CFloat(Trim(Replace(resbl!ht, ",", ".")))
    $grdv[j, i].Text = Format(fsomme, "#######0.00")
    fsomme = 0
    Try fsomme = CFloat(Replace($grdv[j, i + 1].Text, ",", ".")) + CFloat(Trim(Replace(resbl!qte, ",", ".")))
    $grdv[j, i + 1].Text = Format(fsomme, "######" & $sdec)
    fsomme = 0
    If $stype = "F" Then
      If resbl!type = "A" Or resbl!type = "E" Then
        Try fsomme = CFloat(Replace($grdv[j, i + 2].Text, ",", ".")) + CFloat(resbl!marge)
      Else
        Try fsomme = CFloat(Replace($grdv[j, i + 2].Text, ",", ".")) + CFloat(resbl!margem)
      Endif
    Else
      Try fsomme = CFloat(Replace($grdv[j, i + 2].Text, ",", ".")) + CFloat(resbl!marge)
    Endif 
    $grdv[j, i + 2].Text = Format(fsomme, "#####0.00")
    
    resbl.MoveNext
  Next
  
End

Public Sub razcol()
  
  Dim i, j As Integer
  
  For i = 0 To 11
    For j = 0 To 12
      $grdv[j, i].text = "0"
      If Even(j) Then
        $grdv[j, i].Background = Color.Background       
      Else 
        $grdv[j, i].Background = Color.Background - 100
      Endif
      $grdv[j, i].Alignment = Align.Right
      If j = 12 Then $grdv[j, i].Background = &HDFBFFF
    Next
  Next
  
End

Private Function code_Read() As String
  
  Return $scode
  
End

Private Sub code_Write(Value As String)
  
  $scode = Value
  
End

Private Function dec_Read() As String
  
  Return $sdec
  
End

Private Sub dec_Write(Value As String)
  
  $sdec = Value
  
End
